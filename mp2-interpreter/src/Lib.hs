module Lib where
import Data.HashMap.Strict as H (HashMap, empty, fromList, insert, lookup, union)


--- Data Types
--- ----------

--- ### Environments and Results

type Env  = H.HashMap String Val
type PEnv = H.HashMap String Stmt

type Result = (String, PEnv, Env)

--- ### Values

data Val = IntVal Int
         | BoolVal Bool
         | CloVal [String] Exp Env
         | ExnVal String
    deriving (Eq)

instance Show Val where
    show (IntVal i) = show i
    show (BoolVal i) = show i
    show (CloVal xs body env) = "<" ++ show xs   ++ ", "
                                    ++ show body ++ ", "
                                    ++ show env  ++ ">"
    show (ExnVal s) = "exn: " ++ s

--- ### Expressions

data Exp = IntExp Int
         | BoolExp Bool
         | FunExp [String] Exp
         | LetExp [(String,Exp)] Exp
         | AppExp Exp [Exp]
         | IfExp Exp Exp Exp
         | IntOpExp String Exp Exp
         | BoolOpExp String Exp Exp
         | CompOpExp String Exp Exp
         | VarExp String
    deriving (Show, Eq)

--- ### Statements

data Stmt = SetStmt String Exp
          | PrintStmt Exp
          | QuitStmt
          | IfStmt Exp Stmt Stmt
          | ProcedureStmt String [String] Stmt
          | CallStmt String [Exp]
          | SeqStmt [Stmt]
    deriving (Show, Eq)

--- Primitive Functions
--- -------------------

intOps :: H.HashMap String (Int -> Int -> Int)
intOps = H.fromList [ ("+", (+))
                    , ("-", (-))
                    , ("*", (*))
                    , ("/", (div))
                    ]

boolOps :: H.HashMap String (Bool -> Bool -> Bool)
boolOps = H.fromList [ ("and", (&&))
                     , ("or", (||))
                     ]

compOps :: H.HashMap String (Int -> Int -> Bool)
compOps = H.fromList [ ("<", (<))
                     , (">", (>))
                     , ("<=", (<=))
                     , (">=", (>=))
                     , ("/=", (/=))
                     , ("==", (==))
                     ]

--- Problems
--- ========

--- Lifting Functions
--- -----------------

liftIntOp :: (Int -> Int -> Int) -> Val -> Val -> Val
liftIntOp op (IntVal x) (IntVal y) = IntVal $ op x y
liftIntOp _ _ _ = ExnVal "Cannot lift"

liftBoolOp :: (Bool -> Bool -> Bool) -> Val -> Val -> Val
liftBoolOp op (BoolVal x) (BoolVal y)= BoolVal $ op x y
liftBoolOp _ _ _ = ExnVal "Cannot lift"

liftCompOp :: (Int -> Int -> Bool) -> Val -> Val -> Val
liftCompOp op (IntVal x) (IntVal y)= BoolVal $ op x y
liftCompOp _ _ _ = ExnVal "Cannot lift"

--- Eval
--- ----

eval :: Exp -> Env -> Val

--- ### Constants

eval (IntExp i)  _ = IntVal i
eval (BoolExp i) _ = BoolVal i

--- ### Variables

eval (VarExp s) env = 
   case H.lookup s env of
      Just v ->v
      Nothing ->ExnVal "No match in env"

--- ### Arithmetic

eval (IntOpExp op e1 e2) env = 
      let v1= eval e1 env
          v2= eval e2 env
       in case H.lookup op intOps of
            Just f
              |op=="/" && v2==(IntVal 0) -> ExnVal "Division by 0"
              |otherwise ->liftIntOp f v1 v2
            Nothing ->ExnVal "Wrong op"
             

--- ### Boolean and Comparison Operators

eval (BoolOpExp op e1 e2) env = 
      let v1= eval e1 env
          v2= eval e2 env
       in case H.lookup op boolOps of
            Just f ->liftBoolOp f v1 v2
            Nothing ->ExnVal "Wrong op"

eval (CompOpExp op e1 e2) env =
      let v1= eval e1 env
          v2= eval e2 env
       in case H.lookup op compOps of
            Just f ->liftCompOp f v1 v2
            Nothing ->ExnVal "Wrong op"

--- ### If Expressions

eval (IfExp e1 e2 e3) env =
     let cond=eval e1 env
     in case cond of
       BoolVal True ->eval e2 env
       BoolVal False ->eval e3 env
       _ ->ExnVal "Condition is not a Bool"

--- ### Functions and Function Application

eval (FunExp params body) env = CloVal params body env

eval (AppExp e1 args) env = 
    case eval e1 env of
         CloVal params body clenv -> eval body new_env
            where 
              argVals=[eval arg env| arg<-args]
              new_env=H.union (H.fromList (zipWith (\p v->(p,v)) params argVals)) clenv
         _ ->ExnVal "Apply to non-closure"
         
    

--- ### Let Expressions

eval (LetExp pairs body) env = 
  let new_env= H.union (H.fromList [(x, eval v env)| (x,v)<-pairs]) env
  in eval body new_env

--- Statements
--- ----------

-- Statement Execution
-- -------------------

exec :: Stmt -> PEnv -> Env -> Result
exec (PrintStmt e) penv env = (val, penv, env)
    where val = show $ eval e env

--- ### Set Statements

exec (SetStmt var e) penv env = ("",penv,H.insert var (eval e env) env)

--- ### Sequencing

exec (SeqStmt []) penv env = ("",penv,env)
exec (SeqStmt (s:ss)) penv env = 
    let (p1,penv1,env1)=exec s penv env
        (p2,penv2,env2)=exec (SeqStmt ss) penv1 env1
    in (p1++p2,penv2,env2)

              

--- ### If Statements

exec (IfStmt e1 s1 s2) penv env =
     let b=eval e1 env
     in case b of 
        BoolVal True->exec s1 penv env
        BoolVal False->exec s2 penv env
        _ -> ("exn: Condition is not a Bool",penv,env)

--- ### Procedure and Call Statements

exec p@(ProcedureStmt name args body) penv env = ("",H.insert name (ProcedureStmt name args body) penv , env)

exec (CallStmt name args) penv env = 
  case H.lookup name penv of
     Just (ProcedureStmt name params body)->
            exec body penv new_env
            where 
              argVals=[eval arg env| arg<-args]
              new_env=H.union (H.fromList (zipWith (\p v->(p,v)) params argVals)) env
     Nothing ->("Procedure " ++ name ++" undefined",penv,env)
    
   
